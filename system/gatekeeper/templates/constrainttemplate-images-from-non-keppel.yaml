apiVersion: templates.gatekeeper.sh/v1
kind: ConstraintTemplate
metadata:
  name: gkimagesfromnonkeppel
spec:
  crd:
    spec:
      names:
        kind: GkImagesFromNonKeppel

  targets:
    - target: admission.k8s.gatekeeper.sh
      rego: |
        package imagesfromnonkeppel

        # This violation is usually only detectable on the
        # deployment/daemonset/statefulset level since Tugger will fix the
        # image reference on the pod level.

        violation_owners = {"Deployment", "DaemonSet", "StatefulSet", "Job"}
        pod_owners       = {"ReplicaSet", "DaemonSet", "StatefulSet", "Job"}

        iro := input.review.object
        iro_support_group := object.get(iro, ["metadata", "labels", "cc/support-group"], "none")
        iro_service := object.get(iro, ["metadata", "labels", "cc/service"], "none")

        pod_belongs_to[kind] {
          ref := iro.metadata.ownerReferences[_]
          kind := ref.kind
          kind == pod_owners[_]
        }

        containers[container] {
          iro.kind == "Pod"
          count(pod_belongs_to) == 0 # otherwise the violation will be reported on the owning object (see `violation_owners`)
          pod_spec := iro.spec

          # We have to use object.get() to get a zero value in case the key doesn't exist
          # because array.concat() only works if both of its arguments are non-nil (i.e.
          # an array, even if it's empty).
          initCntrs := object.get(pod_spec, "initContainers", [])
          container := array.concat(pod_spec.containers, initCntrs)[_]
        }

        containers[container] {
          iro.kind == violation_owners[_]
          pod_spec := iro.spec.template.spec

          # Same as above.
          initCntrs := object.get(pod_spec, "initContainers", [])
          container := array.concat(pod_spec.containers, initCntrs)[_]
        }

        violation[{"msg": msg, "details": {"container": container_name, "image": image}}] {
          container := containers[_]
          container_name := container.name
          image := container.image

          not regex.match("^keppel\\.[^/.]*\\.cloud.sap/", image)
          msg := sprintf(
            "support-group=%s,service=%s: container %q uses an image that is not from a Keppel registry: %s",
            [iro_support_group, iro_service, container_name, image]
          )
        }
